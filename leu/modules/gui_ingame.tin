#nop --
#nop Class Definitions
#nop --

#var gui_ingame-description {Funzioni della gui_ingame}
#var gui_ingame-help {
Gestione dell'interfaccia grafica.\n
Gli elementi sono della gui_ingame sono:\n
title_bar (la barra in alto)\n
bottom_bar (barra in basso)\n
bar_side (barra laterale destra)\n
mappa.\n
}

#nop --
#nop Modloader Stuff
#nop --

#alias gui_ingame-register {
	#nop need to check: events;
	#if {@isloaded{functions}} {
		register_module gui_ingame
	} {
		fail_module gui_ingame
	}
}

#NOP -------------------------------------------------------------------------;
#NOP Ingame graphical structure;
#NOP -------------------------------------------------------------------------;

#VARIABLE {gui_ingame}
{
     {rows}  {40}
     {cols}  {111}
     {top}   {7}
     {bot}   {5}
     {left}  {1}
     {right} {0}
}

#VARIABLE {title_bar}
{
     {toprow} {1}
     {topcol} {1}
}

#VARIABLE {bottom_bar}
{
     {toprow} {-5}
     {topcol} {1}
     {botrow} {-2}
     {botcol} {-1}
}

#NOP -------------------------------------------------------------------------;
#NOP Init the graphical interface;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_ingame_init}
{
    #nop variable set in msdp.tin and gmcp.tin;
    #variable comm_protocol = "NONE";
    map_start;
    #if {$isDetached == 0}
    {
      #nop triggers for one window;
      load_module trigger_attached;
      load_alias_sidebar
    };
    #elseif {$isDetached == 1 }
    {
      #nop triggers for multi windows;
      load_module trigger_detached;
      alias_load_extsidebar
    };
    #elseif {$isDetached == 2}
    {
      
      #system {sh ./bin/extrawindows.sh};
      #nop linux triggers for multi windows;
      load_module trigger_detached;
      alias_load_extsidebar
    };
    #Nop  and the gui;
    gui_ingame_resize;
    gui_ingame_refresh
}

#NOP -------------------------------------------------------------------------;
#NOP Resize the graphical interface;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_ingame_resize}
{
    #SCREEN get rows {gui_ingame[rows]};
    #SCREEN get cols {gui_ingame[cols]};
    #NOP if we have a single window;
    #if {$isDetached == 0} 
    {
      #NOP large screen;
      #if {$gui_ingame[rows] > 30 && $gui_ingame[cols] > 100}
      {
        #SPLIT {$gui_ingame[top]} {$gui_ingame[bot]} {$gui_ingame[left]} -100 1;
        #SCREEN clear all;
        #MAP flag vtmap on;
        #MATH {gui_ingame[right]} {$gui_ingame[cols] - 100};
        #MAP flag vtmap on;
        #MAP offset 18 {-$gui_ingame[right]+3} -10 -3;
        bar_comm_draw_data $bar_comm[active];
        #delay {0.5} {bar_comm_draw_tabs};
        #DRAW white calign box {17} {-$gui_ingame[right]+2} -7 -1;
        #MATH {bar_side[topcol]} {-$gui_ingame[right]+2};
        bar_side_draw_data $bar_side[active];
        #delay {0.5} {bar_side_draw_tabs}
      };
      #NOP mid screen;
      #elseif {$gui_ingame[rows] > 30}
      {
        #MAP flag vtmap on;
        #SCREEN clear all;
        #SPLIT 13 {$gui_ingame[bot]} 0 0 1;
        #MAP offset 8 1 14 -1;
        #VAR {bar_comm[botrow]} 7;
        #delay {0.5} {bar_comm_draw_tabs}
      };
      #NOP small screen;
      #else 
      {
        #MAP flag vtmap off;
        #SCREEN clear all; 
        #SPLIT 1 5 0 0 1
      }
    };
    #NOP if we have a detached windows; 
    #else
    { 
      #NOP large screen;
      #if {$gui_ingame[rows] > 30 && $gui_ingame[cols] > 80}
      {
        #var gui_ingame[top] 1;
        #SPLIT {$gui_ingame[top]} {$gui_ingame[bot]};
        #SCREEN clear all;
        #MAP flag vtmap off;    
      };
      #NOP medium/small screen; 
      #else
      {
        #MAP flag vtmap off;
        #SCREEN clear all;
        #SPLIT 1 {$gui_ingame[bot]} 0 0 1
      }
    }
}

#NOP -------------------------------------------------------------------------;
#NOP Update the gui;
#NOP -------------------------------------------------------------------------;
#ALIAS {gui_ingame_refresh}
{
      title_bar_draw;
      bottom_bar_draw
}

#nop -------------------------------------------------------------------------;
#nop Draw some buttons over the health bar.;
#nop -------------------------------------------------------------------------;
#ALIAS {title_bar_draw}
{
    #line ignore #showme {\e]68;2;TAB;help aaa_servizio\a\e[4;24m*Leu help\e[24m} {1} {-28};
    #line ignore #showme {\e]68;2;TAB;help help\a\e[4;24m*Lumett help\e[24m} {1} {-18};
    #line ignore #showme {\e]68;2;TAB;#end\a\e[4;24m*Esci\e[24m} {1} {-5}
}

#nop -------------------------------------------------------------------------;
#nop Draw a health bar.;
#nop -------------------------------------------------------------------------;
#EVENT {RECEIVED LINE}
{
  #if {$comm_protcol == "MSDP"} 
  {
      bottom_bar_draw
  }
}


#alias {bottom_bar_draw}
{
  #if {$comm_protcol == "MSDP"} 
  {
    #math {hp_percent} {100 * $MSDP_HEALTH / $MSDP_HEALTH_MAX};
    #math {hpbar1}   {$hp_percent / 10};
    #math {hpbar2}   {10 - $hpbar1};
    #math {mana_percent} {100 * $MSDP_MANA / $MSDP_MANA_MAX};
    #math {manabar1}   {$mana_percent / 10};
    #math {manabar2}   {10 - $manabar1};
    #math {mov_percent} {100 * $MSDP_MOVEMENT / $MSDP_MOVEMENT_MAX};
    #math {movbar1}   {$mov_percent / 10};
    #math {movbar2}   {10 - $movbar1};

    #FORMAT {hpbar} {<011>%+${hpbar1}s<099><000>%+${hpbar2}s<099>};
    #FORMAT {manabar} {<015>%+${manabar1}s<099><000>%+${manabar2}s<099>};
    #FORMAT {movbar} {<012>%+${movbar1}s<099><000>%+${movbar2}s<099>};

    #FORMAT prompt1 {  %s * hp:[%+5s/%-5s]} $hpbar $MSDP_HEALTH $MSDP_HEALTH_MAX;
    #FORMAT prompt2 {  %s * ma:[%+5s/%-5s]} $manabar $MSDP_MANA $MSDP_MANA_MAX;
    #FORMAT prompt3 {  %s * mv:[%+5s/%-5s]} $movbar $MSDP_MOVEMENT $MSDP_MOVEMENT_MAX;

    #if {&MSDP_HEALTH}
    {
      #LINE ignore #SHOWME {$prompt1 Lv : $MSDP_LEVEL Exp: $MSDP_EXPERIENCE Gld: $MSDP_MONEY]} {-5};
      #if {&MSDP_OPPONENT_NAME} 
      {
         #LINE ignore #SHOWME {$prompt2 Trg: $MSDP_OPPONENT_NAME Thp: $MSDP_OPPONENT_HEALTH Stato: $button_status_trg} {-4};
      };
      #else
      {
	     #LINE ignore #SHOWME {$prompt2 Trg: nessuno Stato: na} {-4};
      };
      #LINE ignore #SHOWME {$prompt3 Stato: $button_status_tnk Spl: $incanti} {-3};
    };
    #else 
    {
      #LINE ignore #SHOWME { ATTENDO I DATI } {$bottom_bar[botrow]-3};
      #LINE ignore #SHOWME {   DI MSDP } {$bottom_bar[botrow]-2};
      msdp_ask_bottombar
    };
    #DRAW white calign box {$bottom_bar[toprow]-1} {$bottom_bar[topcol]} {$bottom_bar[botrow]} {$bottom_bar[botcol]}
  };
  #else
  {  
    #math {hp_percent} {100 * $gmcp[char][vitals][hp] / $gmcp[char][vitals][maxhp]};
    #math {hpbar1}   {$hp_percent / 10};
    #math {hpbar2}   {10 - $hpbar1};
    #math {mana_percent} {100 * $gmcp[char][vitals][mana] / $gmcp[char][vitals][maxmana]};
    #math {manabar1}   {$mana_percent / 10};
    #math {manabar2}   {10 - $manabar1};
    #math {pow_percent} {100 * $gmcp[char][vitals][pow] / $gmcp[char][vitals][maxpow]};
    #math {powbar1}   {$pow_percent / 10};
    #math {powbar2}   {10 - $powbar1};

    #FORMAT {hpbar} {<011>%+${hpbar1}s<099><000>%+${hpbar2}s<099>};
    #FORMAT {manabar} {<015>%+${manabar1}s<099><000>%+${manabar2}s<099>};
    #FORMAT {powbar} {<012>%+${powbar1}s<099><000>%+${powbar2}s<099>};

    #FORMAT prompt1 {  %s * hp:[%+5s/%-5s]} $hpbar $gmcp[char][vitals][hp] $gmcp[char][vitals][maxhp];
    #FORMAT prompt2 {  %s * ma:[%+5s/%-5s]} $manabar $gmcp[char][vitals][mana] $gmcp[char][vitals][maxmana];
    #FORMAT prompt3 {  %s * pw:[%+5s/%-5s]} $powbar $gmcp[char][vitals][pow] $gmcp[char][vitals][maxpow];

    #FORMAT prompt4 {%.5s} $gmcp[char][vitals][fighting][name];
    #FORMAT prompt4b { opp_nm:[ %-5s ]} $prompt4;
    #FORMAT prompt5 { opp_hp:[ %-5s ]} $gmcp[char][vitals][fighting][hp];
    #FORMAT prompt6 { opp_lv:[ %-5s ]} $gmcp[char][vitals][fighting][level];

    #FORMAT prompt7 { room_pos:[ %s ]} $gmcp[char][vitals][roomPos];
    #FORMAT prompt8 { char_gld:[ %s ]} $gmcp[char][base][gold];    

    #LINE ignore #SHOWME {$prompt1 $prompt4b $prompt7} {-5};
    #LINE ignore #SHOWME {$prompt2 $prompt5 $prompt8} {-4};
    #LINE ignore #SHOWME {$prompt3 $prompt6} {-3};

    #DRAW white calign box {$bottom_bar[toprow]-1} {$bottom_bar[topcol]} {$bottom_bar[botrow]} {$bottom_bar[botcol]}
  } 
}

#NOP -------------------------------------------------------------------------;
#NOP Inpuit line in zmud style;
#NOP -------------------------------------------------------------------------;

#CONFIG REPEAT_ENTER ON

#VARIABLE last_input {}

#EVENT {HISTORY UPDATE}
{
    #DELAY 0 #LINE ignore #SHOWME {<aaf><FFF><788>%0<088>} {-1};
    #VARIABLE last_input {%0}
}

#EVENT {PROCESSED KEYPRESS}
{
	#if {{%1} != {10} && {$last_input} !== {}}
	{
		#VARIABLE last_input {};
		#LINE ignore #SHOWME {} {-1};
		#cursor end;
		#history insert {}
	};
}

#NOP -------------------------------------------------------------------------;
#NOP Execute speedwalks with .;
#NOP -------------------------------------------------------------------------;

#alias {.%0}
{
	#var cnt {};

	#parse {%0} {char}
	{
		#if {"$char" >= "0" && "$char" <= "9"}
		{
			#var cnt $cnt$char
		};
		#elseif {"$cnt" == ""}
		{
			#send $char
		};
		#else
		{
			#$cnt #send $char;
			#var cnt {}
		};
	}
}

#ALIAS {button_status %1}
{
    #DRAW white calign box -6 49 -4 59 {"%1"};
}
