#nop --
#nop Class Definitions
#nop --

#var msdp-description {Abilita il Mud Server Data Protocol}
#var msdp-help {
<179>Questo modulo deve essere caricato prima della connessione al mud.<099>\n
Le informazioni sono salvate in MSDP_%*.\n
}

#nop --
#nop Modloader Stuff
#nop --

#alias msdp-register {
	#if {@isloaded{functions}} {
		register_module msdp
	} {
		#showme ERROR: functions module not loaded;
		#class msdp kill
	}
}

#VARIABLE {MSDP}
{
     {ARRAY_CLOSE} {\x06}
     {ARRAY_OPEN} {\x05}
     {TABLE_CLOSE} {\x04}
     {TABLE_OPEN} {\x03}
     {VAL} {\x02}
     {VAR} {\x01}
}

#VARIABLE {MSDP_REPORTABLE_VARIABLES}
{
    {CHARACTER_NAME} {}
    {HEALTH} {}
    {HEALTH_MAX} {}
    {MANA} {}
    {MANA_MAX} {}
    {MONEY} {}
    {MOVEMENT} {}
    {MOVEMENT_MAX} {}
    {LEVEL} {}
    {WORLD_TIME} {}
    {OPPONENT_HEALTH} {}
    {OPPONENT_HEALTH_MAX} {}
    {OPPONENT_LEVEL} {}
    {OPPONENT_NAME} {}
    {EXPERIENCE} {}
    {ALIGNEMENT} {}
    {SCREEN_FOCUS} {}
    {SCREEN_HEIGHT} {}
    {SCREEN_LOCATION_HEIGHT} {}
    {SCREEN_LOCATION_WIDTH} {}
    {SCREEN_MINIMIZED} {}
    {SCREEN_WIDTH} {}
}

#NOP -------------------------------------------------------------------------;
#NOP Gestione degli Alias MSDP;
#NOP -------------------------------------------------------------------------;

#ALIAS {msdp_report}
{
     #NOP Ask the server to report all keys inside REPORTABLE_VARIABLES;
     #VARIABLE result {$TELNET[IAC]$TELNET[SB]$TELNET[MSDP]$MSDP[VAR]REPORT};
     #foreach {*REPORTABLE_VARIABLES[]} {tmp}
     {
          #VARIABLE result {$result$MSDP[VAL]$tmp}
     };
     #SEND {$result$TELNET[IAC]$TELNET[SE]\}
}

#ALIAS {msdp_list}
{
     #SEND {$TELNET[IAC]$TELNET[SB]$TELNET[MSDP]$MSDP[VAR]LIST$MSDP[VAL]%0$TELNET[IAC]$TELNET[SE]\}
}

#ALIAS {msdp_send}
{
     #SEND {$TELNET[IAC]$TELNET[SB]$TELNET[MSDP]$MSDP[VAR]SEND$MSDP[VAL]%0$TELNET[IAC]$TELNET[SE]\}
}

#ALIAS {msdp_ask_bottombar}
{
    msdp_send HEALTH;
    msdp_send HEALTH_MAX;
    msdp_send MANA;
    msdp_send MANA_MAX;
    msdp_send MOVEMENT;
    msdp_send MOVEMENT_MAX;
    msdp_send MONEY;
    msdp_send LEVEL;
    msdp_send EXPERIENCE
}

#nop -------------------------------------------------------------------------;
#nop EVENTI VARI;
#nop -------------------------------------------------------------------------;

#EVENT {IAC WILL MSDP}
{
    #SEND {$TELNET[IAC]$TELNET[DO]$TELNET[MSDP]\};
    #delay 1 msdp_report;
    #class trigger_data kill;
    #class trigger_data read $saves/triggerslive.txt;
    #variable comm_protcol "MSDP"
}

#EVENT {IAC SB MSDP}
{
    #VARIABLE {MSDP_%0} {%1}
}

#EVENT {IAC SB MSDP SCREEN_WIDTH}
{
    gui_refresh;
}

#EVENT {IAC SB MSDP SCREEN_HEIGHT}
{
    gui_refresh
}

#EVENT {IAC SB MSDP AC}
{
    gui_refresh
}